ZSH_TMUX_AUTOSTART=true

# Uncomment following line if you want red dots to be displayed while waiting for completion
export COMPLETION_WAITING_DOTS="true"

# Automatically quote globs in URL and remote references
# http://superuser.com/questions/420525/scp-with-zsh-no-matches-found
__remote_commands=(scp rsync)
autoload -U url-quote-magic
zle -N self-insert url-quote-magic
zstyle -e :urlglobber url-other-schema '[[ $__remote_commands[(i)$words[1]] -le ${#__remote_commands} ]] && reply=("*") || reply=(http https ftp)'

plugins=(colored-man-pages docker key-bindings osx tmux)

unsetopt share_history

fpath=(/usr/local/share/zsh-completions $fpath)

export EDITOR=nvim
export VISUAL=nvim

export GOPATH=$HOME/projects
export PATH=$PATH:$GOPATH/bin
unset LSCOLORS

if [ -e /usr/local/share/zsh/site-functions/_aws ]; then
  source /usr/local/share/zsh/site-functions/_aws
fi

autoload -Uz vcs_info

zstyle ':vcs_info:*' stagedstr '%F{green}●'
zstyle ':vcs_info:*' unstagedstr '%F{yellow}●'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{11}%r'
zstyle ':vcs_info:*' enable git svn

theme_precmd () {
  if [[ -z $(git ls-files --other --exclude-standard 2> /dev/null) ]] {
      zstyle ':vcs_info:*' formats ' %b%c%u%B%F{green}'
  } else {
      zstyle ':vcs_info:*' formats ' %b%c%u%B%F{red}●%F{green}'
  }

  vcs_info
}

start_time=$SECONDS
function prompt_paradox_preexec {
  start_time=$SECONDS
}

time_precmd () {
  timer_result=$(($SECONDS-$start_time))
  if [[ $timer_result -gt 5 ]]; then
    calc_elapsed_time
  else
    unset timer_result
  fi
  start_time=$SECONDS
}

calc_elapsed_time () {
  if [[ $timer_result -ge 3600 ]]; then
    let "timer_hours = $timer_result / 3600"
    let "remainder = $timer_result % 3600"
    let "timer_minutes = $remainder / 60"
    let "timer_seconds = $remainder % 60"
    timer_result="${timer_hours}h${timer_minutes}m${timer_seconds}s"
  elif [[ $timer_result -ge 60 ]]; then
    let "timer_minutes = $timer_result / 60"
    let "timer_seconds = $timer_result % 60"
    timer_result="${timer_minutes}m${timer_seconds}s"
  else
    timer_result="${timer_result}s"
  fi
}

setopt prompt_subst
PROMPT='%F{cyan}${timer_result} %{$fg_bold[blue]%}%c%{$reset_color%}%B%F{green}${vcs_info_msg_0_}%B%F{magenta} %{$reset_color%}%% '

autoload -U add-zsh-hook
add-zsh-hook precmd  theme_precmd
add-zsh-hook precmd  time_precmd

idunno()
{
  echo -n "¯\_(ツ)_/¯" | pbcopy
  echo "¯\_(ツ)_/¯"
}

fixaudio ()
{
   sudo pkill -i coreaudio
   sudo pkill -i spotify
}

fixvolume ()
{
  osascript -e "set volume input volume 100"
}

wtf () {
  cat << EOF
(ノ ゜Д゜)ノ ︵ ┻━┻
CTRL + Q - binding for tmux
CTRL + U – Cuts text up until the cursor
CTRL + K – Cuts text from the cursor until the end of the line
CTRL + Y – Pastes text
CTRL + E – Move cursor to end of line
CTRL + A – Move cursor to the beginning of the line
ALT + F – Jump forward to next space
ALT + B – Skip back to previous space
ALT + Backspace – Delete previous word
CTRL + W – Cut word behind cursor
EOF
}

notify () {
  pusher -i 2093 DONE
}

if [ -e ~/.waffle.env ]; then
  source ~/.waffle.env
fi

# GPG Agent stuffs
# https://gist.github.com/bmhatfield/cc21ec0a3a2df963bffa3c1f884b676b
if [ -f ~/.gnupg/.gpg-agent-info ] && [ -n "$(pgrep gpg-agent)" ]; then
  source ~/.gnupg/.gpg-agent-info
  export GPG_AGENT_INFO
else
  eval $(gpg-agent --daemon --write-env-file ~/.gnupg/.gpg-agent-info)
fi

source <(kubectl completion zsh)

# The next line updates PATH for the Google Cloud SDK.
if [ -f '/Users/skajo02/google-cloud-sdk/path.zsh.inc' ]; then source '/Users/skajo02/google-cloud-sdk/path.zsh.inc'; fi

# The next line enables shell command completion for gcloud.
if [ -f '/Users/skajo02/google-cloud-sdk/completion.zsh.inc' ]; then source '/Users/skajo02/google-cloud-sdk/completion.zsh.inc'; fi

fuckincpuhog () {
  output=$(ps -e -o pid,pri,pcpu,comm | grep -i vshield)
  echo ${output}
  process_count=$(ps -e -o pid,comm | grep -i vshield | wc -l)
  echo "Processes Found: ${process_count}"
  ps -e -o pid,comm | grep -i vshield | grep -v grep | awk {'print $1'} | xargs sudo renice 20
  output=$(ps -e -o pid,pri,pcpu,comm | grep -i vshield)
  echo ${output}
}

export NVM_DIR="$HOME/.nvm"
. "/usr/local/opt/nvm/nvm.sh"

export PATH="/usr/local/opt/mongodb@3.2/bin:$PATH"


if (( ${+terminfo[smkx]} )) && (( ${+terminfo[rmkx]} )); then
  function zle-line-init() {
    echoti smkx
  }
  function zle-line-finish() {
    echoti rmkx
  }
  zle -N zle-line-init
  zle -N zle-line-finish
fi

bindkey -e                                            # Use emacs key bindings

bindkey '^r' history-incremental-search-backward      # [Ctrl-r] - Search backward incrementally for a specified string. The string may begin with ^ to anchor the search to the beginning of the line.
if [[ "${terminfo[kpp]}" != "" ]]; then
  bindkey "${terminfo[kpp]}" up-line-or-history       # [PageUp] - Up a line of history
fi
if [[ "${terminfo[knp]}" != "" ]]; then
  bindkey "${terminfo[knp]}" down-line-or-history     # [PageDown] - Down a line of history
fi

# start typing + [Up-Arrow] - fuzzy find history forward
if [[ "${terminfo[kcuu1]}" != "" ]]; then
  autoload -U up-line-or-beginning-search
  zle -N up-line-or-beginning-search
  bindkey "${terminfo[kcuu1]}" up-line-or-beginning-search
fi
# start typing + [Down-Arrow] - fuzzy find history backward
if [[ "${terminfo[kcud1]}" != "" ]]; then
  autoload -U down-line-or-beginning-search
  zle -N down-line-or-beginning-search
  bindkey "${terminfo[kcud1]}" down-line-or-beginning-search
fi

if [[ "${terminfo[khome]}" != "" ]]; then
  bindkey "${terminfo[khome]}" beginning-of-line      # [Home] - Go to beginning of line
fi
if [[ "${terminfo[kend]}" != "" ]]; then
  bindkey "${terminfo[kend]}"  end-of-line            # [End] - Go to end of line
fi

if [[ "${terminfo[kcbt]}" != "" ]]; then
  bindkey "${terminfo[kcbt]}" reverse-menu-complete   # [Shift-Tab] - move through the completion menu backwards
fi

bindkey 'M-f' emacs-forward-word
bindkey '\ef' emacs-backward-word
